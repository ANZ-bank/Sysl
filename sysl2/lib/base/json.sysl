!grammar json:
    object: "{" (string ":" value):"," "}"

    array: "[" value:"," "]"

    value:
        string
        number
        object
        array
        "true"
        "false"
        "null"

    string:
        /"(
            [^"\\\n\{Cc}]  # \{Cc} = control characters
            |
            \\( ["\\\/bfnrt] | u[0-9a-fA-F]{4} )
        )*"/

    number:
        /
            -?
            (?: 0 | [1-9]\d* )
            ( \.\d+ )?
            ( [eE] [-+]? \d+ )?
        /

!grammar jsonx:
    !mixin json

    object: "{" (string ":" value):"," ","? "}"

    array: "[" value:"," ","? "]"
