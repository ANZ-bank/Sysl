# Generates an entity-relationship diagram from the tables of model.sysl.

let model = //{./sysl}.normalize(//{./sysl}.load('out/model.pb'));

# Transforms a relation model into a set of table specs.
let toTables = \m
    m.types <&> m.fields nest |fieldName,fieldType,fk,opt,patterns,pk|columns
;

# Returns '|' if a field is required, or 'o' if not.
# Used to draw FK relationship cardinality of the target.
let zo = \f cond {f.opt: 'o', _: '|'};

# Returns an array of strings representing columns as entity fields.
let toRows = \cols cols orderby .fieldName >> 
    $`${cond !.opt || .pk {true: '*'}}${.fieldName} : ${.fieldType}${cond {.fk: ' <<FK>>'}}`;

# Transforms tables to an entity-relationship diagram.
let toErd = \tables $`
    @startuml
    skinparam component {
      BackgroundColor FloralWhite
      BorderColor Black
      ArrowColor Crimson
    }
    skinparam stereotypeCBackgroundColor Orchid
    hide methods

    ${tables orderby .typeName >> \(:typeName, columns:cols, ...) $`
    entity ${typeName} {
      ${toRows(cols where .pk) ++ cond {cols where .pk: ['--']} ++ toRows(cols where !.pk)::\i}
    }`
    ::\n\n}

    ${tables orderby .typeName >> \(:typeName, :columns, ...)
        let fks = columns where .fk orderby .;
        $`${fks >> $`${typeName:-15s} }o--${zo(.)}| ${.fk.type}`::\i}`
    ::\i}
    @enduml
`;

toErd(toTables(model))
