# Performs transformations of Sysl models, producing "better" Sysl models.

# Loads and returns a Sysl model.
let load = \path
    let syslDescriptor = //encoding.proto.descriptor(//os.file('sysl.pb'));
    //encoding.proto.decode(syslDescriptor, 'Module' , //os.file(path));

# Extracts the set of pattern attributes of an element.
let patterns = \x x.attrs?('patterns')?.a.elt:{} => .@item.s;

# A macro to convert `ret` statement payload string to structured tuples.
# TODO: handle dynamic type (int, float etc)
let payloadMacro = (
    @grammar: {://grammar.lang.wbnf:
        payload -> (status=("ok"|"error"|[1-5][0-9][0-9]) "<:"?)? type=[^ \t\[]* attr?;
        attr -> %!Array(nvp|modifier);
        nvp_item -> ('"' ([^"\\] | [\\][\\brntu'"])* '"' | "'" ([^''])* "'") | %!Array(nvp_item);
        nvp ->  name=\w+ "=" nvp_item;
        modifier -> "~" name=[\w\+]+;
        .wrapRE -> /{\s*()\s*};
        .macro Array(child) {
            "[" (child):"," "]"
        }
    :},
    @transform: (payload: //{./util}.simpleTransform),
);

# TODO: this is assuming that attribute values are similar to arrai values
let rec buildNvp = \nvp cond nvp {
    (nvp_item: i, ...): i => (:.@, @item: buildNvp(.@item)),
    _: //eval.value(//seq.join('', nvp.''))
};
# Converts a return statement payload into a tuple of its parts.
let parseReturn = \payload //{./util}.invokeMacro(payloadMacro, payload) +> (
    attr: cond //grammar.parse(payloadMacro.@grammar, 'payload', payload).attr?:{} {
        {}: (),
        attr: (
                nvp: attr.nvp?:{} => (@: .@item.name.'', @value: buildNvp(.@item.nvp_item)),
                modifier: attr.modifier?:{} => .@item.name.''
            )
    }
);

# Replaces all `ret` payloads with structured tuples.
let polishReturns = \m
    let tweak = \.
        . +> (ret: .ret +> (payload: parseReturn(.ret.payload)));

    m +> (apps: m.apps?:{} >> \a 
        a +> (endpoints: a.endpoints?:{} >> \p 
            p +> (stmt: p.stmt?:[] >> \s
                cond s.ret?:{} {{}: s, _: tweak(s)}
            )
        )
    )
;

# Applies polish functions to the appropriate elements of a Sysl model.
let polish = \m
    polishReturns(m)
    # TODO: More polish.
;

# Returns a normalised, relational model of a Sysl model.
let normalize = \m
    let apps = m.apps => \(@:appName, @value:app)
        (
            :appName,
            appPatterns: patterns(app),
        )
    ;
    let endpoints = (m.apps => \(@:appName, @value:app)
        app.endpoints?:{} => \(@:epName, ...)
            (
                :appName,
                :epName,
            )
    ) -> //rel.union(.);
    let stmts = (m.apps => \(@:appName, @value:app)
        (app.endpoints?:{} => \(@:epName, @value:ep)
            ep.stmt?:{} =>
                (
                    :appName,
                    :epName,
                    stmt: .@item +> (
                        # TODO: Use stmt.source_context once populated by parser.
                        # https://github.com/anz-bank/sysl/issues/990
                        source_context: ep.source_context +> (:.@),
                    ),
                )
        ) -> //rel.union(.)
    ) -> //rel.union(.);
    let calls = stmts where .stmt.call?:{} => \(:appName, :epName, :stmt)
        (
            from: (
                :appName,
                :epName,
            ),
            to: (
                appName: stmt.call.target.part.@item,
                epName: stmt.call.endpoint,
            ),
            :stmt.source_context,
        )
    ;
    let types = (m.apps => \(@:appName, @value:app)
        app.types?:{} => \(@:typeName, @value: t)
            (
                :appName,
                :typeName,
            )
        ) -> //rel.union(.) where .
    ;
    let fields = (m.apps => \(@:appName, @value:app)
        (app.types?:{} => \(@:typeName, @value:type)
            type.relation?.attr_defs?:{} => \(@:fieldName, @value:field)
                let patterns = patterns(field);
                (
                    :appName,
                    :typeName,
                    :fieldName,
                    fieldType: cond field {
                        (primitive:p, ...): p,
                        (type_ref:(ref:(path:p), ...), ...): //seq.join('.', p),
                        _: '?',
                    },
                    fk: cond field.type_ref?.ref.path:{} {
                        [t, f]: (:appName, typeName: t, fieldName: f)
                    },
                    :patterns,
                    pk: !!({'pk'} & patterns),
                    opt: field.opt?:{},
                )
        ) -> //rel.union(.) where .
    ) -> //rel.union(.);

    (
        :apps,
        :endpoints,
        :calls,
        :types,
        :fields,
        # TODO:
        # :annotations, (aka attributes)
        :stmts,
        # :tags, (aka patterns)
    )
;

# Export the functions from the module.
(
    :load,
    :normalize,
    :parseReturn,
    :patterns,
    :polish,
)
