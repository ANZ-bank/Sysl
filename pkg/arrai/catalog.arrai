# Generates a catalog of pages describing the applications in model.sysl.

let sysl = //{./sysl};
let model = sysl.newNormalize(//{./sysl}.load('out/model.pb'));

let appAnnoValue = \appName \appAnno \annoName
    appAnno where .appName = appName && .appAnnoName = annoName => .appAnnoValue orderby .;

let appPackage = \appName \appAnno appAnnoValue(appName, appAnno, 'package');
let appDescription = \appName \appAnno appAnnoValue(appName, appAnno, 'description');

let appPatterns = \appName \tag tag where .appName = appName => .appTag;

let appNameStr = \appName //seq.join(' :: ', appName);

# Transforms the model's information about the named package to a page of HTML content.
let toPackagePage = \m \specificAppPackage
    let apps = m.app where appPackage(.appName, m.anno.app) = specificAppPackage;
    let dbs = apps where appPatterns(.appName, m.tag.app) & {'db'};
    let endpoints = apps <&> m.ep;
    let types = apps <&> m.type;

    $`
    <h1>${specificAppPackage::}</h1>

    <h2>Integration Diagram</h2>
    <img src="TODO" />

    <h2>Database Index</h2>
    <table border="1">
    <tr>
        <th>Database Application Name</th>
    </tr>
    ${dbs => $`
        <tr>
            <td>${appNameStr(.appName)}</td>
        </tr>
    ` orderby .::\i}
    </table>

    <h2>Application Index</h2>
    <table border="1">
    <tr>
        <th>Application Name</th>
        <th>Method</th>
    </tr>
    ${endpoints => $`
        <tr>
            <td>${appNameStr(.appName)}</td>
            <td>${.epName}</td>
        </tr>
    ` orderby .::\i}
    </table>

    <h2>Type Index</h2>
    <table border="1">
    <tr>
        <th>Application Name</th>
        <th>Type Name</th>
    </tr>
    ${apps <&> m.type => $`
        <tr>
            <td>${appNameStr(.appName)}</td>
            <td>${.typeName}</td>
        </tr>
    ` orderby .::\i}
    </table>

    <h1>Databases</h1>
    ${dbs => $`
        <h2>Database ${appNameStr(.appName)}</h2>
    ` orderby .::\i}

    <h1>Applications</h1>
    ${apps => $`
        <h2>Application ${appNameStr(.appName)}</h2>
        <p>${appDescription(.appName, m.anno.app)::}</p>
        ${endpoints => $`
            <h3>${appNameStr(.appName)} ${.epName}</h3>
            <p>Sequence diagram</p>
            <p>Request types</p>
            <p>Response types</p>
        ` orderby .::\i}
    ` orderby .::\i}

    <h1>Types</h1>
    <ul>
    ${types => $`
        <li>
            <p>${appNameStr(.appName)}.${.typeName}</p>
            <p>Fields:</p>
            <table border="1">
            <tr>
                <th>Field name</th>
                <th>Type</th>
                <th>Description</th>
            </tr>
            ${{.} <&> m.field => $`
                <tr>
                    <td>${.fieldName}</td>
                    <td>${sysl.resolvedType(.fieldType)}</td>
                    <td></td>
                </tr>
            ` orderby .::\i}
            </table>
        </li>
    ` orderby .::\i}
    `
;

# Transforms the model to a map of output filename to HTML page content.
let toCatalog = \m
    let packages = m.app => appPackage(.appName, m.anno.app) where . && !//seq.has_prefix('google_', //seq.join("", .));
    packages => (@:$`${.::}.html`, @value: toPackagePage(m, .))
;

toCatalog(model)
