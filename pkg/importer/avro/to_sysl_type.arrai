# Transform Avro record to Sysl record
let util = //{./avro_util};

# Build type annotation like [union=["int32", "null"]]
let buildTypeAnnotation = \type
    cond type {
        (a: typeArray):
            let types = (typeArray >> util.getTypeName(.)) >> .s;
            cond //seq.contains(['null'])(types) {
                true:
                    cond //seq.has_prefix(['null'], types) {
                        true: # types is ['null', 'string']
                            '',
                        _: # types is ['string', 'null']
                            $'union=${types}'
                    }
                ,
                _: ''
            }
        ,
        _: ''
    }
;

let buildLogicalTypeAnnotation = \.
    cond .('type') {
        {'type': typeName, 'logicalType': logicalTypeName, ...}:
            cond logicalTypeName.s {
                'uuid': '[~uuid]',
                'date': '',
                'time-millis': ['~time-millis'],
                'time-micros': ['~time-micros'],
                'timestamp-millis': ['~timestamp-millis'],
                'timestamp-micros': ['~timestamp-micros'],
                'local-timestamp-millis': ['~local-timestamp-millis'],
                'local-timestamp-micros': ['~local-timestamp-micros'],
                'duration': ['~fixed', 'fixed_size="12"', '~duration'],
                _: []
            },
        _: []
    }
;

let buildAnnotations = \.
    buildLogicalTypeAnnotation(.) + 
    [
        buildTypeAnnotation(.('type')),
        util.getAnnotationS(., 'doc'),
        util.getDefaultVal(.),
        util.getAnnotationS(., 'order'),
    ]
;

let transformer = \record
$`${
    cond record {
        {'isMap':(s: 'true'), ...}:
            cond record {
                {"name": (s: typeName), ...}:
                    $`${'\n'}${util.indent}!type String${util.transformType(record)}Item ${
                        util.prettyAnnotations(['json_map_key="key"', util.getDefaultVal(record)])
                    }:${'\n'}${util.indent}${util.indent}key <: string${
                    '\n'}${util.indent}${util.indent}value <: ${util.transformType(record)}${'\n\n'}`,
            },
    }
}${
cond record {
{'type': (s: 'record'), ...}:
$`${util.indent}!type ${record('name').s} ${
    util.prettyAnnotations([util.getAnnotationS(record, 'namespace'), util.getAnnotationS(record, 'doc')])
}:
${
    let fields = record('fields')? .a:[]; 
    fields >> 
        ($`${util.indent}${util.indent}${.('name').s} <: ${util.transformType(.('type'))} ${
                util.prettyAnnotations(buildAnnotations(.))
            }${
                let aliases = .('aliases')? .a:[];
                cond aliases {
                    []: '',
                    _: $`${'\n'}${aliases >> \alias 
                                    $`${util.indent}${util.indent}${alias.s} <: ${util.transformType(.('type'))} ${
                                        util.prettyAnnotations(buildAnnotations(.) + [$`alias_of="${.('name').s}"`])}`::\i}`
                }
            }`)::
}${
let aliases = record('aliases')? .a:[];
cond aliases {
    [...]: $`${'\n\n'}${
        aliases >> ($`${util.indent}!alias ${.s}:${'\n'}${util.indent}${util.indent}${record('name').s}`)::\i\n}${'\n'}`
    }
}`}}`;transformer
