let util = //{./avro_util};

let transformer = \aliasItem
    cond aliasItem {
        {'type': (s: 'fixed'), 'name': (s: name), "logicalType": (s: "duration"), ...}:
            '', # Ignore logicalType duration.
        {'type': (s: 'fixed'), 'name': (s: name), ...}: $`
            !alias ${util.correctName(name)}${
                let annotations = util.prettyAnnotations(
                    ['~fixed', $`fixed_size="${aliasItem('size')}"`] ++ (['namespace', 'name'] >> util.getAnnotationS(aliasItem, .))
                );
                cond {annotations: ' ' + $`${annotations}`}
            }:
                bytes
            ${
                let aliases = aliasItem('aliases')? .a:[];
                cond {
                    aliases count > 0: $`
                        
                        ${aliases >> \alias $`
                            !alias ${util.correctName(alias.s)}${
                                let annotations = util.prettyAnnotations([util.getAnnotationS(alias, 'name')]);
                                cond {annotations: ' ' + $`${annotations}`}
                            }:
                                ${util.correctName(name)}
                        `::\n\n}
                    `,
                }
            }
        `,
        # Double check.
        {'type': (s: 'array'), 'items': (s: itemsType), ...}: $`
            !alias ${util.correctName(itemsType)}Sequence${
                util.prettyAnnotations([util.defaultVal(.)])
            }:
                sequence of ${itemsType}
        `,
    }
;

transformer
