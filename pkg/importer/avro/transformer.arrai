let util = //{./util};

# Extra items transformed to Sysl type !enum
let rec extraEnums = \schema
  cond schema {
    {'type': (s: 'enum'), 'name':(s: name), ...}:
      {schema},
    {'type': (s: 'record'), 'name':(s: name), "fields": (a : fields), ...}:
      //rel.union(fields => extraEnums(.@item("type"))),
    {'type': (s: 'array'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record array
      //rel.union(fields => extraEnums(.@item("type"))),
    {'type': (s: 'map'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record map
      //rel.union(fields => extraEnums(.@item("type"))), 
    (a: types):
      //rel.union(types => .@item => extraEnums(.))
  }
;

# Extra items transformed to Sysl type !type 
let rec extraTypes = \schema 
  cond schema {
    {"type": (s: "record"), "name": (s: name), "fields": (a : fields), ...}:
      {schema} | //rel.union(fields => extraTypes(.@item("type"))),
    {'type': (s: 'array'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record array
      {schema('items')} | //rel.union(fields => extraTypes(.@item("type"))),
    {'type': (s: 'map'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record map
      {schema('items') | {'isMap': (s: 'true')}} | //rel.union(fields => extraTypes(.@item("type"))),
    {'type': (a: types)}: # type is an array
      //rel.union(types => .@item => extraTypes(.)),  
    (a: types): 
      //rel.union(types => .@item => extraTypes(.)),
};

# Extra items transformed to Sysl type !alias
let rec extraAliases = \schema 
  cond schema {
    {"type": (s: "record"), "name": (s: name), "fields": (a : fields), ...}:
      //rel.union(fields => extraAliases(.@item("type"))),
    {'type': (s: 'array'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record array
      //rel.union(fields => extraAliases(.@item("type"))),
    {'type': (s: 'map'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record map
      //rel.union(fields => extraAliases(.@item("type"))),
    {'type': (a: types)}: # type is an array
      //rel.union(types => .@item => extraAliases(.)),  
    (a: types): 
      //rel.union(types => .@item => extraAliases(.)),
    {'type': (s: 'fixed'), ...}: # fixed type
      {schema},
};

# Extra item transformed to Sysl type !union
let rec extraUnions = \schema
  cond schema {
    (a:[...]):
      {schema.a} | //rel.union(
        schema.a => .@item => \.
          cond . {
              {'type': (s: 'record'), "fields": (a : fields), ...}:
                //rel.union(fields => extraUnions(.@item("type"))),
              {'type': (s: 'array'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record array
                //rel.union(fields => extraUnions(.@item("type"))),
              {'type': (s: 'map'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record map
                //rel.union(fields => extraUnions(.@item("type"))),
          }
        ),
    {'type': (s: 'record'), "fields": (a : fields), ...}:
      //rel.union(fields => extraUnions(.@item("type"))),
    {'type': (s: 'array'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record array
      //rel.union(fields => extraUnions(.@item("type"))),
    {'type': (s: 'map'), 'items': {'type': (s: 'record'), "fields": (a : fields), ...}, ...}: # record map
      //rel.union(fields => extraUnions(.@item("type"))),
  }
;

let avroTransform = \schema \appName \packageName
  # Load Avro spec whose format is protobuf 
  let schema = //encoding.json.decode(//os.file(schema));

$'
##########################################
##                                      ##
##  AUTOGENERATED CODE -- DO NOT EDIT!  ##
##                                      ##
##########################################
${appName}${util.prettyAnnotations(['avro_spec="1.0"',$'${cond packageName {'':'', _:$'package="${packageName}"'}}'])}:
${util.indent}# Types
${extraTypes(schema) => //{./transformToSyslType}(.) orderby . ::}
${util.indent}# Aliases
${extraAliases(schema) => //{./transformToSyslAlias}(.) orderby . ::}
${util.indent}# Unions
${
# It applies //{./transformUnionToSysl}(.) to every item and will remove duplidated items if //{./transformUnionToSysl}(.)
# produces, as it iterates set.
extraUnions(schema) => //{./transformToSyslUnion}(.) orderby . ::}
${util.indent}# Enums
${extraEnums(schema) => //{./transformToSyslEnum}(.) orderby . ::}
'
;avroTransform
